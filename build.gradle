allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

project(':common-domain') {
    dependencies {
        compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
    }
}

project(':custom-domain') {
    dependencies {
        compile project(':common-domain')
    }
}

project(':custom-dao') {
    sourceSets {
        generated {
            java {
                srcDirs = ['src/main/generated']
            }
        }
    }

    configurations {
        metamodel
    }

    dependencies {
        compile project(':custom-domain')
        compile 'org.hsqldb:hsqldb:2.3.1'
        compile 'org.hibernate:hibernate-entitymanager:4.3.0.Final'
        compile 'com.mysema.querydsl:querydsl-apt:3.3.0'
        compile('com.mysema.querydsl:querydsl-jpa:3.3.0') {
            exclude group: 'org.hibernate.javax.persistence'
        }
        metamodel 'org.hibernate:hibernate-jpamodelgen:4.3.0.Final'
    }

    task generateQueryDslMetamodel(type: GenerateMetaModel) {
        dependencies = [project(':common-domain'), project(':custom-domain')]
        annotationProcessor = GenerateMetaModel.QUERY_DSL_ANNOTATION_PROCESSOR
    }

    task generateJpaMetamodel(type: GenerateMetaModel) {
        dependencies = [project(':common-domain'), project(':custom-domain')]
        annotationProcessor = GenerateMetaModel.JPA_ANNOTATION_PROCESSOR
    }

    idea {
        module {
            sourceDirs += file('src/main/generated')
        }
    }

    compileJava {
        dependsOn generateQueryDslMetamodel, generateJpaMetamodel
        source sourceSets.generated.java.srcDirs
    }

    clean {
        delete sourceSets.generated.java.srcDirs
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

class GenerateMetaModel extends JavaCompile {
    public static final String JPA_ANNOTATION_PROCESSOR = 'org.hibernate.jpamodelgen.JPAMetaModelEntityProcessor'
    public static final String QUERY_DSL_ANNOTATION_PROCESSOR = 'com.mysema.query.apt.jpa.JPAAnnotationProcessor'

    GenerateMetaModel() {
        classpath = project.configurations.compile + project.configurations.metamodel
        destinationDir = project.sourceSets.generated.java.srcDirs.iterator().next() as File
    }

    void setDependencies(Iterable<Project> projects) {
        setSource(projects.collect { project ->
            project.sourceSets.main.java
        }.flatten())
    }

    void setAnnotationProcessor(String processor) {
        options.compilerArgs = [
                "-proc:only",
                "-processor", processor
        ]
    }
}